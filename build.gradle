/*
 * Copyright 2012 - 2014 Silvio Wangler (silvio.wangler@gmail.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *          http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
	jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath "com.eriwen:gradle-js-plugin:1.12.1"
        classpath 'com.eriwen:gradle-css-plugin:1.11.1'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${gradleBintrayVersion}"
    }
}

configure(allprojects) {

    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: 'jacoco'

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        all*.exclude group: 'log4j', module: 'log4j'
    }

    repositories {
        jcenter()
    }

    group = 'ch.silviowangler.dox'
    version = '0.5.3'

    configurations.all {
        resolutionStrategy {

            failOnVersionConflict()

            eachDependency { DependencyResolveDetails details ->
                //changing 'groovy-all' into 'groovy':
                if (details.requested.name == 'groovy-all') {
                    details.useTarget group: details.requested.group, name: 'groovy', version: groovyVersion
                }
            }

            forcedModules = [
                    "org.springframework:spring-aop:${springVersion}",
                    "org.springframework:spring-asm:${springVersion}",
                    "org.springframework:spring-beans:${springVersion}",
                    "org.springframework:spring-context:${springVersion}",
                    "org.springframework:spring-core:${springVersion}",
                    "org.springframework:spring-expression:${springVersion}",
                    "org.springframework:spring-jdbc:${springVersion}",
                    "org.springframework:spring-orm:${springVersion}",
                    "org.springframework:spring-test:${springVersion}",
                    "org.springframework:spring-tx:${springVersion}",
                    "org.springframework:spring-web:${springVersion}",
                    "org.springframework:spring-webmvc:${springVersion}",
                    "org.springframework:spring-oxm:${springVersion}",
                    "org.hamcrest:hamcrest-core:${hamcrestVersion}",
                    "org.hibernate:hibernate-entitymanager:${hibernateVersion}",
                    "commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}",
                    "commons-lang:commons-lang:2.4",
                    "org.slf4j:slf4j-api:${slf4jVersion}",
                    "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
                    "joda-time:joda-time:${jodaTimeVersion}",
                    "org.aspectj:aspectjrt:${aspectjWeaverVersion}",
                    "org.aspectj:aspectjweaver:${aspectjWeaverVersion}"
            ]
        }
    }

    idea.module {
        inheritOutputDirs = true
    }
}

configure(subprojects) { subproject ->

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    compileJava.options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']
    compileTestJava.options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']

    sourceCompatibility = 7
    targetCompatibility = 7

    dependencies {
        testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
        testCompile "org.spockframework:spock-core:${spockVersion}"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        testCompile "junit:junit:${junitVersion}",
                "org.mockito:mockito-core:${mockitoVersion}",
                "org.hamcrest:hamcrest-core:${hamcrestVersion}",
                "org.hamcrest:hamcrest-library:${hamcrestVersion}"
    }

    jar {
        manifest {
            attributes(
                    'Built-By': "Gradle ${gradle.gradleVersion}",
                    'Implementation-Title': "DOX-${subproject.name}",
                    'Implementation-Version': subproject.version,
                    'Implementation-Vendor': 'Silvio Wangler',
                    'Specification-Vendor': 'Silvio Wangler',
                    'Specification-Title': "DOX-${subproject.name}",
                    'Specification-Version': subproject.version
            )
        }

        from("${rootProject.projectDir}") {
            include "LICENSE.txt"
            into "META-INF"
            expand(dateOfYear: new Date().format('yyyy'))
        }

        baseName = "dox-${baseName}"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava

        from("${rootProject.projectDir}") {
            include "LICENSE.txt"
            into "META-INF"
            expand(dateOfYear: new Date().format('yyyy'))
        }
    }

    artifacts {
        archives sourcesJar
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            include '**/*.properties'
            filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                    'dox.app.version': "${version}-${new Date().format("yyyyMMddHHmmss")}".toString(),
                    'gradleVersion': gradle.gradleVersion
            ])
        }
        from(sourceSets.main.resources.srcDirs) { exclude '**/*.properties' }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }
            }
        }
    }

    install {
        repositories.mavenInstaller {
            customizePom(pom, subproject)
        }
    }

    bintray {
        user = bintrayUser // this usually comes form gradle.properties file in ~/.gradle
        key = bintrayKey // this usually comes form gradle.properties file in ~/.gradle
        publications = ['mavenJava'] // see publications closure
        pkg { //package will be created if does not exist
            repo = 'releases'
            name = 'dox'
            desc = 'Package created from dox'
            licenses = ['Apache-2.0']
            labels = ['spring', 'dms', 'rest', 'angularjs', 'documentmanagement']
        }
    }
}

project(':api') {
    dependencies {
        compile "com.google.guava:guava:${guavaVersion}",
                "joda-time:joda-time:${jodaTimeVersion}"
    }
}

project(':core') {
    dependencies {
        compile project(':api'), project(':domain'), project(':inexport'),
                "org.springframework:spring-context:${springVersion}",
                "org.springframework:spring-aop:${springVersion}",
                "commons-codec:commons-codec:${commonsCodecVersion}",
                "com.itextpdf:itextpdf:${iTextVersion}",
                "commons-io:commons-io:${commonsIOVersion}",
                "org.apache.commons:commons-exec:${commonsExec}",
                "joda-time:joda-time:${jodaTimeVersion}",
                "com.google.guava:guava:${guavaVersion}",
                "com.googlecode.flyway:flyway-core:${flywayVersion}"
        compile("org.springframework.security:spring-security-core:${springSecurityVersion}") {
            exclude group: 'org.springframework', module: 'spring-beans'
        }
        compile("commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}") {
            exclude group: 'commons-logging', module: 'commons-logging'
        }
        compile "org.springframework.security:spring-security-config:${springSecurityVersion}"
        runtime "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
                "ch.qos.logback:logback-classic:${logbackVersion}",
                "org.springframework.security:spring-security-config:${springSecurityVersion}",
                "org.aspectj:aspectjweaver:${aspectjWeaverVersion}", // need this version because 1.6.x is buggy with Spring 3.x (http://goo.gl/73Pwi)
                "org.bouncycastle:bcprov-jdk15on:${bouncycastleVersion}", "org.bouncycastle:bcpkix-jdk15on:${bouncycastleVersion}"
        testCompile "org.springframework:spring-test:${springVersion}",
                "commons-io:commons-io:${commonsIOVersion}",
                "com.google.guava:guava:${guavaVersion}"
        testRuntime "com.h2database:h2:${h2Version}"
    }

    test {
        exclude '**/*IntegrationTest.class'
        reports.html.destination = file("${reports.html.destination}/unit2")
        reports.junitXml.destination = file("${reports.junitXml.destination}/unit2")
    }

    task integrationTest(type: Test) {
        include '**/*IntegrationTest.class'
        reports.html.destination = file("${reports.html.destination}/integration")
        reports.junitXml.destination = file("${reports.junitXml.destination}/integration")

        doFirst {
            def doxStore = System.getenv('DOX_STORE')

            if (!doxStore) doxStore = System.properties['java.io.tmpdir']

            def thumbnailDir = new File(doxStore, 'thumbnails')

            final thumbnailDirExists = thumbnailDir.exists()
            logger.info("File ${thumbnailDir.absolutePath} exists? ${thumbnailDirExists}")

            if (!thumbnailDirExists) {
                assert thumbnailDir.mkdir()
            }
        }
    }

    jacocoTestReport {

        if (file("${buildDir}/jacoco/integrationTest.exec").exists()) {
            executionData = files("${buildDir}/jacoco/integrationTest.exec", "${buildDir}/jacoco/test.exec")
        }
        additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    }

    check.dependsOn integrationTest
}

project(':web') {
    apply plugin: 'war'
    apply plugin: "com.eriwen.gradle.js"
    apply plugin: "com.eriwen.gradle.css"

    war.archiveName = "dox-${version}.war"

    dependencies {
        compile project(':api'), project(':inexport'), project(':core')
        compile "commons-io:commons-io:${commonsIOVersion}"
        compile "org.codehaus.groovy:groovy-templates:${groovyVersion}"
        compile "org.springframework:spring-webmvc:${springVersion}"
        compile("org.springframework.security:spring-security-web:${springSecurityVersion}") {
            exclude group: 'org.springframework', module: 'spring-tx'
            exclude group: 'org.springframework', module: 'spring-jdbc'
        }
        compile("org.springframework.security:spring-security-taglibs:${springSecurityVersion}") {
            exclude group: 'org.springframework', module: 'spring-tx'
            exclude group: 'org.springframework', module: 'spring-jdbc'
        }
        testCompile "org.springframework:spring-test:${springVersion}"
        compile "org.springframework.mobile:spring-mobile-device:${springMobileVersion}"
        compile "com.google.guava:guava:${guavaVersion}"
        compile "joda-time:joda-time:${jodaTimeVersion}"
        compile "eu.bitwalker:UserAgentUtils:${userAgentVersion}"
        runtime "org.apache.tiles:tiles-core:${tilesVersion}"
        runtime "org.apache.tiles:tiles-servlet:${tilesVersion}"
        runtime "org.apache.tiles:tiles-jsp:${tilesVersion}"
        runtime "org.apache.tiles:tiles-template:${tilesVersion}"
        runtime "ch.qos.logback:logback-access:${logbackVersion}"
        runtime 'javax.servlet:jstl:1.2'
        runtime "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
        compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:${jacksonVersion}"
        compile "org.springframework:spring-oxm:${springVersion}"
        runtime "com.thoughtworks.xstream:xstream:${xstreamVersion}"
        runtime 'joda-time:joda-time-jsptags:1.1.1'
        providedRuntime "com.h2database:h2:${h2Version}"
        providedRuntime "mysql:mysql-connector-java:${mysqlVersion}"
        providedCompile 'javax.servlet.jsp:jsp-api:2.1'
        providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    }

    javascript.source {
        dev {
            js {
                srcDirs file("${projectDir}/src/main/webapp/resources/js/libs/angular-1.4.8"), file("${projectDir}/src/main/webapp/resources/js/dox")
                include "*.js"
                exclude "*.min.js*"
            }
        }
        prod {
            js {
                srcDirs file("${projectDir}/src/main/webapp/resources/js/dox")
                include "*.min.js*"
            }
        }
    }

    combineJs {
        source = javascript.source.dev.js.files
        dest = file("${buildDir}/dox-all.js")
    }

    def googleCompileOptions = new com.google.javascript.jscomp.CompilerOptions()
    googleCompileOptions.languageIn = 'ECMASCRIPT5'

    minifyJs {
        source = combineJs
        dest = file("${buildDir}/gen/js/dox-all-min.js")

        closure {
            warningLevel = 'QUIET'
            compilerOptions = googleCompileOptions

        }
    }

    gzipJs {
        source = minifyJs
        dest = file("${buildDir}/gen/js/dox-all-min-${version}.js")
    }

    css.source {
        dev {
            css {
                srcDir file("${projectDir}/src/main/webapp/resources/css")
                include "*.css"
                exclude "*.min.css"
            }
        }
    }

    combineCss {
        source = css.source.dev.css.files
        dest = "${buildDir}/all.css"
    }

    minifyCss {
        source = combineCss
        dest = "${buildDir}/gen/css/all-min.css"
        yuicompressor { // Optional
            lineBreakPos = -1
        }
    }

    gzipCss {
        source = minifyCss
        dest = "${buildDir}/gen/css/all.${version}.css"
    }

    war {
        it.dependsOn minifyCss, minifyJs

        from("${buildDir}/gen/js", {
            into 'js'
        })

        from("${buildDir}/gen/css", {
            into 'resources/css'
        })
    }
}

project(':inexport') {
    dependencies {
        compile project(':api'),
                "org.springframework:spring-context:${springVersion}"
        compile "com.thoughtworks.xstream:xstream:${xstreamVersion}", { transitive = false }
    }
}


idea.project {
    jdkName = '1.7'
    languageLevel = '1.7'

    // update the source wildcards
    wildcards += '!?*.txt;!?*.pdf;!?*.xml;!?*.tif;!?*.sql'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir]) n
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->

        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == 'test'
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            url = 'https://github.com/saw303/dox'
            organization {
                name = 'Wangler Software Development'
                url = 'http://saw303.github.io/dox/'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url = 'https://github.com/saw303/dox'
                connection = 'scm:git:git://github.com/saw303/dox'
                developerConnection = 'scm:git:git://github.com/saw303/dox'
            }
            developers {
                developer {
                    id = 'saw303'
                    name = 'Silvio Wangler'
                    email = 'silvio.wangler@gmail.com'
                }
            }
            issueManagement {
                system = 'Youtrack'
                url = 'http://saw303.myjetbrains.com/youtrack/issues/dox'
            }
        }
    }
}
