/*
 * Copyright 2012 - 2014 Silvio Wangler (silvio.wangler@gmail.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *          http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import com.petebevin.markdown.MarkdownProcessor
import org.apache.xalan.xsltc.trax.SAX2DOM
import org.ccil.cowan.tagsoup.Parser
import org.xhtmlrenderer.pdf.ITextRenderer
import org.xml.sax.InputSource

buildscript {
    repositories {
        maven {
            url 'http://repo.wangler.io/repo'
        }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-1'

        classpath 'xalan:xalan:2.7.1'
        classpath "org.markdownj:markdownj:0.3.0-1.0.2b4"
        classpath "org.ccil.cowan.tagsoup:tagsoup:1.2.1"
        classpath "org.xhtmlrenderer:core-renderer:R8"
    }
}

task build <<{
    def source = 'Installation-Guide.md'
    def target = 'Installation-Guide.pdf'

    //Convert from markdown to html
    def mp = new MarkdownProcessor()
    def html = "<html><body>${mp.markdown((new File(source).text))}</body></html>"

    //Convert from html to w3c document
    def parser = new Parser()
    def sax2dom = new SAX2DOM()
    parser.setContentHandler(sax2dom);
    parser.setFeature(Parser.namespacesFeature, false);
    parser.parse(new InputSource(new ByteArrayInputStream(html.getBytes())));

    //Use Document to create pdf
    ITextRenderer renderer = new ITextRenderer();
    renderer.setDocument(sax2dom.getDOM(), null);
    renderer.layout();
    renderer.createPDF((new File(target).newOutputStream()))
}

allprojects {

    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'versions'
    apply plugin: 'jacoco'

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        all*.exclude group: 'log4j', module: 'log4j'
    }

    repositories {
        maven {
            url 'http://repo.wangler.io/repo'
        }
    }

    group = 'ch.silviowangler.dox'
    version = '0.3-M1'

    project.ext {
        springVersion = '4.0.3.RELEASE'
        springMobileVersion = '1.1.1.RELEASE'
        springDataVersion = '1.5.1.RELEASE'
        springSecurityVersion = '3.2.3.RELEASE'
        hibernateVersion = '4.3.5.Final'
        slf4jVersion = '1.7.7'
        logbackVersion = '1.1.2'
        junitVersion = '4.11'
        tilesVersion = '3.0.3'
        commonsIOVersion = '2.4'
        commonsCodecVersion = '1.9'
        commonsBeanUtilsVersion = '1.8.3'
        commonsExec = '1.2'
        jodaTimeVersion = '2.3'
        h2Version = '1.4.177'
        mysqlVersion = '5.1.30'
        iTextVersion = '5.5.0'
        guavaVersion = '16.0.1'
        mockitoVersion = '1.9.5'
        flywayVersion = '2.3.1'
        groovyVersion = '2.2.2'
        xstreamVersion = '1.4.7'
    }

    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    "org.springframework:spring-aop:${project.ext.springVersion}",
                    "org.springframework:spring-asm:${project.ext.springVersion}",
                    "org.springframework:spring-beans:${project.ext.springVersion}",
                    "org.springframework:spring-context:${project.ext.springVersion}",
                    "org.springframework:spring-core:${project.ext.springVersion}",
                    "org.springframework:spring-expression:${project.ext.springVersion}",
                    "org.springframework:spring-jdbc:${project.ext.springVersion}",
                    "org.springframework:spring-orm:${project.ext.springVersion}",
                    "org.springframework:spring-test:${project.ext.springVersion}",
                    "org.springframework:spring-tx:${project.ext.springVersion}",
                    "org.springframework:spring-web:${project.ext.springVersion}",
                    "org.springframework:spring-webmvc:${project.ext.springVersion}",
                    "org.springframework:spring-oxm:${project.ext.springVersion}"
            ]
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'versions'
    apply plugin: 'jacoco'

    compileJava.options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']
    compileTestJava.options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']

    sourceCompatibility = 7
    targetCompatibility = 7

    dependencies {
        compile "org.slf4j:slf4j-api:${project.ext.slf4jVersion}"
        testCompile "junit:junit:${project.ext.junitVersion}",
                "org.mockito:mockito-core:${project.ext.mockitoVersion}",
                'org.hamcrest:hamcrest-core:1.3',
                'org.hamcrest:hamcrest-library:1.3'
    }

    jar {
        manifest {
            attributes(
                    'Built-By': "Gradle ${gradle.gradleVersion}",
                    'Implementation-Title': "DOX-${project.name}",
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': 'Silvio Wangler',
                    'Specification-Vendor': 'Silvio Wangler',
                    'Specification-Title': "DOX-${project.name}",
                    'Specification-Version': project.version
            )
        }

        baseName = "dox-${baseName}"
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            include '**/*.properties' filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                    'dox.app.version': "${version}-${new Date().format("yyyyMMddHHmmss")}".toString(),
                    'gradleVersion': project.gradle.gradleVersion
            ])
        }
        from(sourceSets.main.resources.srcDirs) { exclude '**/*.properties' }
    }
}

project(':api') {
    displayName = 'API module'
    dependencies {
        compile "com.google.guava:guava:${project.ext.guavaVersion}",
                "joda-time:joda-time:${project.ext.jodaTimeVersion}"
    }
}

project(':core') {
    displayName = 'Core module'

    dependencies {
        compile project(':api'), project(':domain'), project(':inexport'),
                "org.springframework:spring-context:${project.ext.springVersion}",
                "org.springframework:spring-aop:${project.ext.springVersion}",
                "commons-codec:commons-codec:${project.ext.commonsCodecVersion}",
                "com.itextpdf:itextpdf:${project.ext.iTextVersion}",
                "commons-beanutils:commons-beanutils:${project.ext.commonsBeanUtilsVersion}",
                "commons-io:commons-io:${project.ext.commonsIOVersion}",
                "org.apache.commons:commons-exec:${project.ext.commonsExec}",
                "joda-time:joda-time:${project.ext.jodaTimeVersion}",
                "com.google.guava:guava:${project.ext.guavaVersion}",
                "com.googlecode.flyway:flyway-core:${project.ext.flywayVersion}"
        compile("org.springframework.security:spring-security-core:${project.ext.springSecurityVersion}") {
            exclude group: 'org.springframework', module: 'spring-beans'
        }
        runtime "org.slf4j:jcl-over-slf4j:${project.ext.slf4jVersion}",
                "ch.qos.logback:logback-classic:${project.ext.logbackVersion}",
                "org.springframework.security:spring-security-config:${project.ext.springSecurityVersion}",
                'org.aspectj:aspectjweaver:1.8.0', // need this version because 1.6.x is buggy with Spring 3.x (http://goo.gl/73Pwi)
                'org.bouncycastle:bcprov-jdk15on:1.50', 'org.bouncycastle:bcpkix-jdk15on:1.50'
        testCompile "org.springframework:spring-test:${project.ext.springVersion}",
                "commons-io:commons-io:${project.ext.commonsIOVersion}",
                "com.google.guava:guava:${project.ext.guavaVersion}"
        testRuntime "com.h2database:h2:${project.ext.h2Version}"
    }

    test {
        exclude '**/*IntegrationTest.class'
        reports.html.destination = file("${reports.html.destination}/unit2")
        reports.junitXml.destination = file("${reports.junitXml.destination}/unit2")
    }

    task integrationTest(type: Test) {
        include '**/*IntegrationTest.class'
        reports.html.destination = file("${reports.html.destination}/integration")
        reports.junitXml.destination = file("${reports.junitXml.destination}/integration")

        doFirst {
            def doxStore = System.getenv('DOX_STORE')

            if (!doxStore) doxStore = System.properties['java.io.tmpdir']

            def thumbnailDir = new File(doxStore, 'thumbnails')

            final thumbnailDirExists = thumbnailDir.exists()
            logger.info("File ${thumbnailDir.absolutePath} exists? ${thumbnailDirExists}")

            if (!thumbnailDirExists) {
                assert thumbnailDir.mkdir()
            }
        }
    }

    jacocoTestReport {

        if (file("${buildDir}/jacoco/integrationTest.exec").exists()) {
            executionData = files("${buildDir}/jacoco/integrationTest.exec", "${buildDir}/jacoco/test.exec")
        }
        additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    }

    check.dependsOn integrationTest
}

project(':web') {
    displayName = 'WEB module'
    apply plugin: 'war'

    war.archiveName = "dox-${version}.war"

    dependencies {
        compile project(':api'), project(':inexport')
        runtime project(':core')
        compile "commons-io:commons-io:${project.ext.commonsIOVersion}"
        compile "org.codehaus.groovy:groovy-templates:${project.ext.groovyVersion}"
        compile "org.springframework:spring-webmvc:${project.ext.springVersion}"
        compile("org.springframework.security:spring-security-web:${project.ext.springSecurityVersion}") {
            exclude group: 'org.springframework', module: 'spring-tx'
            exclude group: 'org.springframework', module: 'spring-jdbc'
        }
        compile("org.springframework.security:spring-security-taglibs:${project.ext.springSecurityVersion}") {
            exclude group: 'org.springframework', module: 'spring-tx'
            exclude group: 'org.springframework', module: 'spring-jdbc'
        }
        testCompile "org.springframework:spring-test:${project.ext.springVersion}"
        compile "org.springframework.mobile:spring-mobile-device:${project.ext.springMobileVersion}"
        compile "com.google.guava:guava:${project.ext.guavaVersion}"
        compile "joda-time:joda-time:${project.ext.jodaTimeVersion}"
        compile "bitwalker:UserAgentUtils:1.12"
        runtime "org.apache.tiles:tiles-core:${project.ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-servlet:${project.ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-jsp:${project.ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-template:${project.ext.tilesVersion}"
        runtime "ch.qos.logback:logback-access:${project.ext.logbackVersion}"
        runtime 'javax.servlet:jstl:1.2'
        runtime 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
        runtime "org.springframework:spring-oxm:${project.ext.springVersion}"
        runtime "com.thoughtworks.xstream:xstream:${project.ext.xstreamVersion}"
        runtime 'joda-time:joda-time-jsptags:1.1.1'
        providedRuntime "com.h2database:h2:${project.ext.h2Version}"
        providedRuntime "mysql:mysql-connector-java:${project.ext.mysqlVersion}"
        providedCompile 'javax.servlet.jsp:jsp-api:2.1'
        providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    }
}

project(':inexport') {
    displayName = 'In- & Export module'
    dependencies {
        compile project(':api'),
                "org.springframework:spring-context:${project.ext.springVersion}"
        compile "com.thoughtworks.xstream:xstream:${project.ext.xstreamVersion}", { transitive = false }
    }
}

idea.project {
    jdkName = '1.7'
    languageLevel = '1.7'

    // update the source wildcards
    wildcards += '!?*.txt;!?*.pdf;!?*.xml;!?*.tif;!?*.sql'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir]) n
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}
