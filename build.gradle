/*
 * Copyright 2012 Silvio Wangler (silvio.wangler@gmail.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
allprojects {

    apply plugin: 'idea'
    apply plugin: 'project-report'

    group = 'ch.silviowangler.dox'
    version = '0.1-SNAPSHOT'

    project.ext {
        springVersion = '3.1.3.RELEASE'
        springDataVersion = '1.2.0.RELEASE'
        springSecurityVersion = '3.1.3.RELEASE'
        hibernateVersion = '4.1.7.Final'
        slf4jVersion = '1.6.6'
        logbackVersion = '1.0.7'
        junitVersion = '4.10'
        tilesVersion = '2.2.2'
        commonsIOVersion = '2.4'
        commonsCodecVersion = '1.6'
        commonsBeanUtilsVersion = '1.8.3'
        jodaTimeVersion = '2.1'
        h2Version = '1.3.168'
        iTextVersion = '5.3.4'
        guavaVersion = '13.0.1'
        mockitoVersion = '1.9.0'
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 7
    targetCompatibility = 7

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://repository.jboss.org/nexus/content/groups/public'
            url 'http://repo.springsource.org/libs-milestone'
            url 'http://repo.springsource.org/libs-snapshot'
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${ext.slf4jVersion}"
        testCompile "junit:junit:${ext.junitVersion}", "org.mockito:mockito-all:${ext.mockitoVersion}"
    }

    jar {
        manifest {
            attributes('Implementation-Title': "DOX : ${project.name}", 'Implementation-Version': project.version)
        }
    }
}

project(':api') {
    dependencies {
        compile "com.google.guava:guava:${ext.guavaVersion}"
    }
}

project(':core') {
    dependencies {
        compile project(':api'), project(':domain'),
                "org.springframework:spring-context:${ext.springVersion}",
                "org.springframework.security:spring-security-core:${ext.springSecurityVersion}",
                "commons-codec:commons-codec:${ext.commonsCodecVersion}",
                "com.itextpdf:itextpdf:${ext.iTextVersion}",
                "commons-beanutils:commons-beanutils:${ext.commonsBeanUtilsVersion}",
                "commons-io:commons-io:${ext.commonsIOVersion}",
                "joda-time:joda-time:${ext.jodaTimeVersion}",
                "com.google.guava:guava:${ext.guavaVersion}"
        runtime "org.slf4j:jcl-over-slf4j:${ext.slf4jVersion}",
                "ch.qos.logback:logback-classic:${ext.logbackVersion}",
                "org.springframework.security:spring-security-config:${ext.springSecurityVersion}"
    }
}

project(':functional-tests') {
    dependencies {
        testCompile project(':core'),
                "org.springframework:spring-test:${ext.springVersion}",
                "commons-io:commons-io:${ext.commonsIOVersion}",
                "com.google.guava:guava:${ext.guavaVersion}"
        runtime "ch.qos.logback:logback-classic:${ext.logbackVersion}",
                "com.h2database:h2:${ext.h2Version}"
    }
}

project(':web') {
    apply plugin: 'war'

    war.archiveName = "dox-${version}.war"

    dependencies {
        compile project(':api')
        runtime project(':core')
        compile "org.springframework:spring-webmvc:${ext.springVersion}"
        testCompile "org.springframework:spring-test:${ext.springVersion}"
        compile 'org.springframework.mobile:spring-mobile-device:1.0.0.RELEASE'
        runtime 'javax.servlet:jstl:1.2'
        compile 'javax.servlet.jsp:jsp-api:2.1'
        compile 'javax.servlet:javax.servlet-api:3.0.1'
        compile "com.google.guava:guava:${ext.guavaVersion}"
        compile "joda-time:joda-time:${ext.jodaTimeVersion}"
        runtime "org.apache.tiles:tiles-core:${ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-servlet:${ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-jsp:${ext.tilesVersion}"
        runtime "org.apache.tiles:tiles-template:${ext.tilesVersion}"
        runtime "ch.qos.logback:logback-access:${ext.logbackVersion}"
        runtime "com.h2database:h2:${ext.h2Version}"
    }
}

idea.project {
    jdkName = '1.7 (Oracle)'
    languageLevel = '1.7'

    // update the source wildcards
    //wildcards += '!?*.txt;!?*.pdf;!?*.xml;!?*.tif'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}
